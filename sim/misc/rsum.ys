# 软件52班 张迁瑞 2015013226
		.pos 0
init:	irmovl Stack, %esp
		irmovl Stack, %ebp
		call   Main   
		halt

		.align 4		# Sample linked list
ele1:
		.long 0x00a
		.long ele2
ele2:
		.long 0x0b0
		.long ele3
ele3:
		.long 0xc00
		.long 0

Main:		pushl	%ebp
		rrmovl	%esp, %ebp
		xorl	%eax, %eax	# init result value
		irmovl	ele1, %edx	# edx=ele1
		pushl	%edx		# set edx as parameter of sum_list
		call	rsum_list
		popl	%edx
		rrmovl	%ebp, %esp
		popl	%ebp
		ret
	
rsum_list:	pushl	%ebp
		rrmovl	%esp, %ebp
		mrmovl	8(%ebp), %edx	# init edx as the start of list
		andl	%edx, %edx	# test if array length is 0
		je	End		# if array length is 0, stop calculating

recursive:
		mrmovl  (%edx), %esi	# esi=current value
		pushl	%esi		# save esi
		irmovl  $4, %edi	# edi=4
		addl	%edi, %edx	# set edx as next
		mrmovl  (%edx), %esi	# get next value to esi
		rrmovl  %esi, %edx	
		pushl	%edx		# edx=parameter for next recursion
		call 	rsum_list
		popl	%edx		 
		popl	%esi		# recover esi
		addl	%esi, %eax	# add current value to total value

End:		rrmovl	%ebp, %esp
		popl	%ebp
		ret


# the stack starts here and grows to lower address
		.pos 0x100
Stack: 
